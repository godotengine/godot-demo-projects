class_name VirtualJoystick
extends Control

## A simple virtual joystick for touchscreens, with useful options.
## Github: https://github.com/MarcoFazioRandom/Virtual-Joystick-Godot

# EXPORTED VARIABLES

## The color of the button when the joystick is pressed.
@export var pressed_color := Color.GRAY

## If the input is inside this range, the output is zero.
@export_range(0, 200, 1) var deadzone_size: float = 10.0

## The max distance the tip can reach.
@export_range(0, 500, 1) var clampzone_size: float = 75.0

enum Joystick_mode {
	FIXED, ## The joystick doesn't move.
	DYNAMIC, ## Every time the joystick area is pressed, the joystick position is set on the touched position.
	FOLLOWING, ## When the finger moves outside the joystick area, the joystick will follow it.
}

## If the joystick stays in the same position or appears on the touched position when touch is started.
@export var joystick_mode := Joystick_mode.FIXED

enum Visibility_mode {
	ALWAYS, ## Always visible.
	TOUCHSCREEN_ONLY, ## Visible on touch screens only.
	WHEN_TOUCHED, ## Visible only when touched.
}

## If the joystick is always visible, or is shown only if there is a touchscreen.
@export var visibility_mode := Visibility_mode.ALWAYS

## If true, the joystick uses Input Actions. (Project -> Project Settings -> Input Map)
@export var use_input_actions: bool = true

@export var action_left: String = "move_left"
@export var action_right: String = "move_right"
@export var action_up: String = "move_forward"
@export var action_down: String = "move_back"

# PUBLIC VARIABLES

## If the joystick is receiving inputs.
var is_pressed: bool = false

# The joystick output.
var output := Vector2.ZERO

# PRIVATE VARIABLES

var _touch_index: int = -1

@onready var _base: TextureRect = $Base
@onready var _tip: TextureRect = $Base/Tip

@onready var _base_default_position: Vector2 = _base.position
@onready var _tip_default_position: Vector2 = _tip.position

@onready var _default_color : Color = _tip.modulate

# FUNCTIONS

func _ready() -> void:
	if not DisplayServer.is_touchscreen_available():
		hide()

	if visibility_mode == Visibility_mode.WHEN_TOUCHED:
		hide()


func _input(input_event: InputEvent) -> void:
	if input_event is InputEventScreenTouch:
		if input_event.pressed:
			if _is_point_inside_joystick_area(input_event.position) and _touch_index == -1:
				if joystick_mode == Joystick_mode.DYNAMIC or joystick_mode == Joystick_mode.FOLLOWING or (
						joystick_mode == Joystick_mode.FIXED and _is_point_inside_base(input_event.position)):
					if joystick_mode == Joystick_mode.DYNAMIC or joystick_mode == Joystick_mode.FOLLOWING:
						_move_base(input_event.position)
					if visibility_mode == Visibility_mode.WHEN_TOUCHED:
						show()
					_touch_index = input_event.index
					_tip.modulate = pressed_color
					_update_joystick(input_event.position)
					get_viewport().set_input_as_handled()
		elif input_event.index == _touch_index:
			_reset()
			if visibility_mode == Visibility_mode.WHEN_TOUCHED:
				hide()
			get_viewport().set_input_as_handled()
	elif input_event is InputEventScreenDrag:
		if input_event.index == _touch_index:
			_update_joystick(input_event.position)
			get_viewport().set_input_as_handled()


func _move_base(new_position: Vector2) -> void:
	_base.global_position = new_position - _base.pivot_offset * get_global_transform_with_canvas().get_scale()


func _move_tip(new_position: Vector2) -> void:
	_tip.global_position = new_position - _tip.pivot_offset * _base.get_global_transform_with_canvas().get_scale()


func _is_point_inside_joystick_area(point: Vector2) -> bool:
	var x: bool = point.x >= global_position.x and point.x <= global_position.x + (size.x * get_global_transform_with_canvas().get_scale().x)
	var y: bool = point.y >= global_position.y and point.y <= global_position.y + (size.y * get_global_transform_with_canvas().get_scale().y)
	return x and y


func _get_base_radius() -> Vector2:
	return _base.size * _base.get_global_transform_with_canvas().get_scale() / 2.0


func _is_point_inside_base(point: Vector2) -> bool:
	var base_radius: Vector2 = _get_base_radius()
	var center: Vector2 = _base.global_position + base_radius
	var vector: Vector2 = point - center
	return vector.length_squared() <= base_radius.x * base_radius.x


func _update_joystick(touch_position: Vector2) -> void:
	var base_radius: Vector2 = _get_base_radius()
	var center: Vector2 = _base.global_position + base_radius
	var vector: Vector2 = touch_position - center
	vector = vector.limit_length(clampzone_size)

	if joystick_mode == Joystick_mode.FOLLOWING and touch_position.distance_to(center) > clampzone_size:
		_move_base(touch_position - vector)

	_move_tip(center + vector)

	if vector.length_squared() > deadzone_size * deadzone_size:
		is_pressed = true
		output = (vector - (vector.normalized() * deadzone_size)) / (clampzone_size - deadzone_size)
	else:
		is_pressed = false
		output = Vector2.ZERO

	if use_input_actions:
		# Release actions.
		if output.x >= 0.0 and Input.is_action_pressed(action_left):
			Input.action_release(action_left)
		if output.x <= 0.0 and Input.is_action_pressed(action_right):
			Input.action_release(action_right)
		if output.y >= 0.0 and Input.is_action_pressed(action_up):
			Input.action_release(action_up)
		if output.y <= 0.0 and Input.is_action_pressed(action_down):
			Input.action_release(action_down)

		# Press actions.
		if output.x < 0.0:
			Input.action_press(action_left, -output.x)
		if output.x > 0.0:
			Input.action_press(action_right, output.x)
		if output.y < 0.0:
			Input.action_press(action_up, -output.y)
		if output.y > 0.0:
			Input.action_press(action_down, output.y)


func _reset() -> void:
	is_pressed = false
	output = Vector2.ZERO
	_touch_index = -1
	_tip.modulate = _default_color
	_base.position = _base_default_position
	_tip.position = _tip_default_position
	# Release actions.
	if use_input_actions:
		for action:String in [action_left, action_right, action_down, action_up]:
			if Input.is_action_pressed(action):
				Input.action_release(action)
